Program   ::= Class ; eof
            | Class ; Program
Class     ::= class type_id { }
            | class type_id { Features }
            | class type_id inherits type_id { }
            | class type_id inherits type_id { Features }
Features  ::= Feature ; Features
            | Feature
Feature   ::= obj_id ( ) : type_id { ExprA }
            | obj_id ( Formals ) : type_id { ExprA }
            | Assign
Formals   ::= Formal , Formals
            | Formal
Formal    ::= obj_id : type_id
ExprA     ::= obj_id <- ExprA
            | ExprB
ExprB     ::= not ExprB
            | ExprC
ExprC     ::= ExprD < ExprC
            | ExprD <= ExprC
            | ExprD = ExprC
            | ExprD
ExprD     ::= ExprE + ExprD
            | ExprE - ExprD
            | ExprE
ExprE     ::= ExprF * ExprE
            | ExprF / ExprE
            | ExprF
ExprF     ::= isvoid ExprF
            | ExprG
ExprG     ::= ~ ExprG
            | ExprH
ExprH     ::= ExprI
            | ExprI ExprH'
ExprH'    ::= @ type_id . obj_id ( )
            | @ type_id . obj_id ( ) ExprH'
            | @ type_id . obj_id ( Exprs )
            | @ type_id . obj_id ( Exprs ) ExprH'
ExprI     ::= ExprJ
            | ExprJ ExprI'
ExprI'    ::= . obj_id ( )
            | . obj_id ( ) ExprI'
            | . obj_id ( Exprs )
            | . obj_id ( Exprs ) ExprI'
ExprJ     ::= ( ExprA )
            | ExprK
ExprK     ::= obj_id ( )
            | obj_id ( Exprs )
            | if ExprA then ExprA else ExprA fi
            | while ExprA loop ExprA pool
            | { ExprA ; }
            | { ExprA ; Exprs' }
            | let Assign in ExprA
            | let Assign Assigns in ExprA
            | case ExprA of Arrow ; esac
            | case ExprA of Arrow ; Arrows esac
            | new type_id
            | obj_id
            | integer
            | string
            | true
            | false
Exprs     ::= ExprA , Exprs
            | ExprA
Exprs'    ::= ExprA ; Exprs'
            | ExprA ;
Assigns   ::= , Assign Assigns
            | , Assign
Assign    ::= obj_id : type_id
            | obj_id : type_id <- ExprA
Arrows    ::= Arrow ; Arrows
            | Arrow ;
Arrow     ::= obj_id : type_id => ExprA
